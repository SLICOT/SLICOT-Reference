<HTML>
<HEAD><TITLE>TB01KD - SLICOT Library Routine Documentation</TITLE>
</HEAD>
<BODY>

<H2><A Name="TB01KD">TB01KD</A></H2>
<H3>
Similarity reduction of system state-matrix to block-diagonal form with two blocks so that the leading block has eigenvalues in a specified domain
</H3>
<A HREF ="#Specification"><B>[Specification]</B></A>
<A HREF ="#Arguments"><B>[Arguments]</B></A>
<A HREF ="#Method"><B>[Method]</B></A>
<A HREF ="#References"><B>[References]</B></A>
<A HREF ="#Comments"><B>[Comments]</B></A>
<A HREF ="#Example"><B>[Example]</B></A>

<P>
<B><FONT SIZE="+1">Purpose</FONT></B>
<PRE>
  To compute an additive spectral decomposition of the transfer-
  function matrix of the system (A,B,C) by reducing the system
  state-matrix A to a block-diagonal form.
  The system matrices are transformed as
  A &lt;-- inv(U)*A*U, B &lt;--inv(U)*B and C &lt;-- C*U.
  The leading diagonal block of the resulting A has eigenvalues
  in a suitably defined domain of interest.

</PRE>
<A name="Specification"><B><FONT SIZE="+1">Specification</FONT></B></A>
<PRE>
      SUBROUTINE TB01KD( DICO, STDOM, JOBA, N, M, P, ALPHA, A, LDA, B,
     $                   LDB, C, LDC, NDIM, U, LDU, WR, WI, DWORK,
     $                   LDWORK, INFO )
C     .. Scalar Arguments ..
      CHARACTER        DICO, JOBA, STDOM
      INTEGER          INFO, LDA, LDB, LDC, LDU, LDWORK, M, N, NDIM, P
      DOUBLE PRECISION ALPHA
C     .. Array Arguments ..
      DOUBLE PRECISION A(LDA,*), B(LDB,*), C(LDC,*), DWORK(*), U(LDU,*),
     $                 WI(*), WR(*)

</PRE>
<A name="Arguments"><B><FONT SIZE="+1">Arguments</FONT></B></A>
<P>

<B>Mode Parameters</B>
<PRE>
  DICO    CHARACTER*1
          Specifies the type of the system as follows:
          = 'C':  continuous-time system;
          = 'D':  discrete-time system.

  STDOM   CHARACTER*1
          Specifies whether the domain of interest is of stability
          type (left part of complex plane or inside of a circle)
          or of instability type (right part of complex plane or
          outside of a circle) as follows:
          = 'S':  stability type domain;
          = 'U':  instability type domain.

  JOBA    CHARACTER*1
          Specifies the shape of the state dynamics matrix on entry
          as follows:
          = 'S':  A is in an upper real Schur form;
          = 'G':  A is a general square dense matrix.

</PRE>
<B>Input/Output Parameters</B>
<PRE>
  N       (input) INTEGER
          The order of the state-space representation,
          i.e. the order of the matrix A.  N &gt;= 0.

  M       (input) INTEGER
          The number of system inputs, or of columns of B.  M &gt;= 0.

  P       (input) INTEGER
          The number of system outputs, or of rows of C.  P &gt;= 0.

  ALPHA   (input) DOUBLE PRECISION.
          Specifies the boundary of the domain of interest for the
          eigenvalues of A. For a continuous-time system
          (DICO = 'C'), ALPHA is the boundary value for the real
          parts of eigenvalues, while for a discrete-time system
          (DICO = 'D'), ALPHA &gt;= 0 represents the boundary value for
          the moduli of eigenvalues.

  A       (input/output) DOUBLE PRECISION array, dimension (LDA,N)
          On entry, the leading N-by-N part of this array must
          contain the unreduced state dynamics matrix A.
          If JOBA = 'S' then A must be a matrix in real Schur form.
          On exit, the leading N-by-N part of this array contains a
          block diagonal matrix inv(U) * A * U with two diagonal
          blocks in real Schur form with the elements below the
          first subdiagonal set to zero.
          The leading NDIM-by-NDIM block of A has eigenvalues in the
          domain of interest and the trailing (N-NDIM)-by-(N-NDIM)
          block has eigenvalues outside the domain of interest.
          The domain of interest for lambda(A), the eigenvalues
          of A, is defined by the parameters ALPHA, DICO and STDOM
          as follows:
          For a continuous-time system (DICO = 'C'):
            Real(lambda(A)) &lt; ALPHA if STDOM = 'S';
            Real(lambda(A)) &gt; ALPHA if STDOM = 'U';
          For a discrete-time system (DICO = 'D'):
            Abs(lambda(A)) &lt; ALPHA if STDOM = 'S';
            Abs(lambda(A)) &gt; ALPHA if STDOM = 'U'.

  LDA     INTEGER
          The leading dimension of array A.  LDA &gt;= MAX(1,N).

  B       (input/output) DOUBLE PRECISION array, dimension (LDB,M)
          On entry, the leading N-by-M part of this array must
          contain the input matrix B.
          On exit, the leading N-by-M part of this array contains
          the transformed input matrix inv(U) * B.

  LDB     INTEGER
          The leading dimension of array B.  LDB &gt;= MAX(1,N).

  C       (input/output) DOUBLE PRECISION array, dimension (LDC,N)
          On entry, the leading P-by-N part of this array must
          contain the output matrix C.
          On exit, the leading P-by-N part of this array contains
          the transformed output matrix C * U.

  LDC     INTEGER
          The leading dimension of array C.  LDC &gt;= MAX(1,P).

  NDIM    (output) INTEGER
          The number of eigenvalues of A lying inside the domain of
          interest for eigenvalues.

  U       (output) DOUBLE PRECISION array, dimension (LDU,N)
          The leading N-by-N part of this array contains the
          transformation matrix used to reduce A to the block-
          diagonal form. The first NDIM columns of U span the
          invariant subspace of A corresponding to the eigenvalues
          of its leading diagonal block. The last N-NDIM columns
          of U span the reducing subspace of A corresponding to
          the eigenvalues of the trailing diagonal block of A.

  LDU     INTEGER
          The leading dimension of array U.  LDU &gt;= max(1,N).

  WR, WI  (output) DOUBLE PRECISION arrays, dimension (N)
          WR and WI contain the real and imaginary parts,
          respectively, of the computed eigenvalues of A. The
          eigenvalues will be in the same order that they appear on
          the diagonal of the output real Schur form of A. Complex
          conjugate pairs of eigenvalues will appear consecutively
          with the eigenvalue having the positive imaginary part
          first.

</PRE>
<B>Workspace</B>
<PRE>
  DWORK   DOUBLE PRECISION array, dimension (LDWORK)
          On exit, if INFO = 0, DWORK(1) returns the optimal value
          of LDWORK.

  LDWORK  INTEGER
          The dimension of working array DWORK.
          LDWORK &gt;= MAX(1,N)   if JOBA = 'S';
          LDWORK &gt;= MAX(1,3*N) if JOBA = 'G'.
          For optimum performance LDWORK should be larger.

</PRE>
<B>Error Indicator</B>
<PRE>
  INFO    INTEGER
          = 0: successful exit;
          &lt; 0: if INFO = -i, the i-th argument had an illegal
               value;
          = 1: the QR algorithm failed to compute all the
               eigenvalues of A;
          = 2: a failure occured during the ordering of the real
               Schur form of A;
          = 3: the separation of the two diagonal blocks failed
               because of very close eigenvalues.

</PRE>
<A name="Method"><B><FONT SIZE="+1">Method</FONT></B></A>
<PRE>
  A similarity transformation U is determined that reduces the
  system state-matrix A to a block-diagonal form (with two diagonal
  blocks), so that the leading diagonal block of the resulting A has
  eigenvalues in a specified domain of the complex plane. The
  determined transformation is applied to the system (A,B,C) as
    A &lt;-- inv(U)*A*U, B &lt;-- inv(U)*B and C &lt;-- C*U.

</PRE>
<A name="References"><B><FONT SIZE="+1">References</FONT></B></A>
<PRE>
  [1] Safonov, M.G., Jonckheere, E.A., Verma, M., Limebeer, D.J.N.
      Synthesis of positive real multivariable feedback systems.
      Int. J. Control, pp. 817-842, 1987.

</PRE>
<A name="Numerical Aspects"><B><FONT SIZE="+1">Numerical Aspects</FONT></B></A>
<PRE>                                  3
  The algorithm requires about 14N  floating point operations.

</PRE>

<A name="Comments"><B><FONT SIZE="+1">Further Comments</FONT></B></A>
<PRE>
  None
</PRE>

<A name="Example"><B><FONT SIZE="+1">Example</FONT></B></A>
<P>
<B>Program Text</B>
<PRE>
*     TB01KD EXAMPLE PROGRAM TEXT
*
*     .. Parameters ..
      INTEGER          NIN, NOUT
      PARAMETER        ( NIN = 5, NOUT = 6 )
      INTEGER          NMAX, MMAX, PMAX
      PARAMETER        ( NMAX = 20, MMAX = 20, PMAX = 20 )
      INTEGER          LDA, LDB, LDC, LDU
      PARAMETER        ( LDA = NMAX, LDB = NMAX, LDC = PMAX,
     $                   LDU = NMAX )
      INTEGER          LDWORK
      PARAMETER        ( LDWORK = 3*NMAX )
*     .. Local Scalars ..
      CHARACTER*1      DICO, JOBA, STDOM
      INTEGER          I, INFO, J, M, N, NDIM, P
      DOUBLE PRECISION ALPHA
*     .. Local Arrays ..
      DOUBLE PRECISION A(LDA,NMAX), B(LDB,MMAX), C(LDC,NMAX),
     $                 DWORK(LDWORK), U(LDU,NMAX), WI(NMAX), WR(NMAX)
*     .. External Subroutines ..
      EXTERNAL         TB01KD
*     .. Executable Statements ..
*
      WRITE ( NOUT, FMT = 99999 )
*     Skip the heading in the data file and read the data.
      READ ( NIN, FMT = '()' )
      READ ( NIN, FMT = * ) N, M, P, ALPHA, DICO, STDOM, JOBA
      IF ( N.LT.0 .OR. N.GT.NMAX ) THEN
         WRITE ( NOUT, FMT = 99990 ) N
      ELSE
         READ ( NIN, FMT = * ) ( ( A(I,J), J = 1,N ), I = 1,N )
         IF ( M.LT.0 .OR. M.GT.MMAX ) THEN
            WRITE ( NOUT, FMT = 99989 ) M
         ELSE
            READ ( NIN, FMT = * ) ( ( B(I,J), J = 1,M ), I = 1, N )
            IF ( P.LT.0 .OR. P.GT.PMAX ) THEN
               WRITE ( NOUT, FMT = 99988 ) P
            ELSE
               READ ( NIN, FMT = * ) ( ( C(I,J), J = 1,N ), I = 1,P )
*              Find the transformed ssr for (A,B,C).
               CALL TB01KD( DICO, STDOM, JOBA, N, M, P, ALPHA, A, LDA,
     $                      B, LDB, C, LDC, NDIM, U, LDU, WR, WI, DWORK,
     $                      LDWORK, INFO )
*
               IF ( INFO.NE.0 ) THEN
                  WRITE ( NOUT, FMT = 99998 ) INFO
               ELSE
                  WRITE ( NOUT, FMT = 99987 ) NDIM
                  WRITE ( NOUT, FMT = 99997 )
                  DO 10 I = 1, N
                     WRITE ( NOUT, FMT = 99994 ) WR(I), WI(I)
   10             CONTINUE
                  WRITE ( NOUT, FMT = 99996 )
                  DO 20 I = 1, N
                     WRITE ( NOUT, FMT = 99995 ) ( A(I,J), J = 1,N )
   20             CONTINUE
                  WRITE ( NOUT, FMT = 99993 )
                  DO 40 I = 1, N
                     WRITE ( NOUT, FMT = 99995 ) ( B(I,J), J = 1,M )
   40             CONTINUE
                  WRITE ( NOUT, FMT = 99992 )
                  DO 60 I = 1, P
                     WRITE ( NOUT, FMT = 99995 ) ( C(I,J), J = 1,N )
   60             CONTINUE
                  WRITE ( NOUT, FMT = 99991 )
                  DO 70 I = 1, N
                     WRITE ( NOUT, FMT = 99995 ) ( U(I,J), J = 1,N )
   70             CONTINUE
               END IF
            END IF
         END IF
      END IF
      STOP
*
99999 FORMAT (' TB01KD EXAMPLE PROGRAM RESULTS',/1X)
99998 FORMAT (' INFO on exit from TB01KD = ',I2)
99997 FORMAT (/' The eigenvalues of state dynamics matrix A are ')
99996 FORMAT (/' The transformed state dynamics matrix inv(U)*A*U is ')
99995 FORMAT (20(1X,F8.4))
99994 FORMAT ( ' (',F8.4,', ',F8.4,' )')
99993 FORMAT (/' The transformed input/state matrix inv(U)*B is ')
99992 FORMAT (/' The transformed state/output matrix C*U is ')
99991 FORMAT (/' The similarity transformation matrix U is ')
99990 FORMAT (/' N is out of range.',/' N = ',I5)
99989 FORMAT (/' M is out of range.',/' M = ',I5)
99988 FORMAT (/' P is out of range.',/' P = ',I5)
99987 FORMAT (' The number of eigenvalues in the domain of interest =',
     $        I5 )
      END
</PRE>
<B>Program Data</B>
<PRE>
 TB01KD EXAMPLE PROGRAM DATA (Continuous system)
  5     2     3    -1.0      C     U     G
  -0.04165    4.9200   -4.9200         0         0
 -1.387944   -3.3300         0         0         0
    0.5450         0         0   -0.5450         0
         0         0    4.9200  -0.04165    4.9200
         0         0         0 -1.387944   -3.3300
         0         0
    3.3300         0
         0         0
         0         0
         0    3.3300
     1     0     0     0     0
     0     0     1     0     0
     0     0     0     1     0
</PRE>
<B>Program Results</B>
<PRE>
 TB01KD EXAMPLE PROGRAM RESULTS

 The number of eigenvalues in the domain of interest =    2

 The eigenvalues of state dynamics matrix A are 
 ( -0.7483,   2.9940 )
 ( -0.7483,  -2.9940 )
 ( -1.6858,   2.0311 )
 ( -1.6858,  -2.0311 )
 ( -1.8751,   0.0000 )

 The transformed state dynamics matrix inv(U)*A*U is 
  -0.7483  -8.6406   0.0000   0.0000   0.0000
   1.0374  -0.7483   0.0000   0.0000   0.0000
   0.0000   0.0000  -1.6858   5.5669   0.0000
   0.0000   0.0000  -0.7411  -1.6858   0.0000
   0.0000   0.0000   0.0000   0.0000  -1.8751

 The transformed input/state matrix inv(U)*B is 
   2.0240  -2.0240
  -1.1309   1.1309
  -0.8621  -0.8621
   2.1912   2.1912
  -1.5555   1.5555

 The transformed state/output matrix C*U is 
   0.6864  -0.0987   0.6580   0.2589   0.9650
  -0.0471   0.6873   0.0000   0.0000  -0.5609
  -0.6864   0.0987   0.6580   0.2589  -0.9650

 The similarity transformation matrix U is 
   0.6864  -0.0987   0.6580   0.2589   0.9650
  -0.1665  -0.5041  -0.2589   0.6580  -0.9205
  -0.0471   0.6873   0.0000   0.0000  -0.5609
  -0.6864   0.0987   0.6580   0.2589  -0.9650
   0.1665   0.5041  -0.2589   0.6580   0.9205
</PRE>

<HR>
<p>
<A HREF=..\libindex.html><B>Return to index</B></A></BODY>
</HTML>
