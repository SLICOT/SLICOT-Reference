<HTML>
<HEAD><TITLE>BB04AD - SLICOT Library Routine Documentation</TITLE>
</HEAD>
<BODY>

<H2><A Name="BB04AD">BB04AD</A></H2>
<H3>
Benchmark examples of (generalized) discrete-time Lyapunov equations
</H3>
<A HREF ="#Specification"><B>[Specification]</B></A>
<A HREF ="#Arguments"><B>[Arguments]</B></A>
<A HREF ="#Method"><B>[Method]</B></A>
<A HREF ="#References"><B>[References]</B></A>
<A HREF ="#Comments"><B>[Comments]</B></A>
<A HREF ="#Example"><B>[Example]</B></A>

<P>
<B><FONT SIZE="+1">Purpose</FONT></B>
<PRE>
  To generate benchmark examples of (generalized) discrete-time
  Lyapunov equations

     T           T
    A  X  A  -  E  X E  =  Y .

  In some examples, the right hand side has the form

             T
    Y  =  - B  B

  and the solution can be represented as a product of Cholesky
  factors

           T
    X  =  U  U .

  E, A, Y, X, and U are real N-by-N matrices, and B is M-by-N. Note
  that E can be the identity matrix. For some examples, B, X, or U
  are not provided.

  This routine is an implementation of the benchmark library
  DTLEX (Version 1.0) described in [1].

</PRE>
<A name="Specification"><B><FONT SIZE="+1">Specification</FONT></B></A>
<PRE>
      SUBROUTINE BB04AD(DEF, NR, DPAR, IPAR, VEC, N, M, E, LDE, A, LDA,
     1                  Y, LDY, B, LDB, X, LDX, U, LDU, NOTE, DWORK,
     2                  LDWORK, INFO)
C     .. Scalar Arguments ..
      CHARACTER         DEF
      CHARACTER*70      NOTE
      INTEGER           INFO, LDA, LDB, LDE, LDU, LDWORK, LDX, LDY, M, N
C     .. Array Arguments ..
      LOGICAL           VEC(8)
      INTEGER           IPAR(*), NR(*)
      DOUBLE PRECISION  A(LDA,*), B(LDB,*), DPAR(*), DWORK(LDWORK),
     1                  E(LDE,*), U(LDU,*), X(LDX,*), Y(LDY,*)

</PRE>
<A name="Arguments"><B><FONT SIZE="+1">Arguments</FONT></B></A>
<P>

<B>Mode Parameters</B>
<PRE>
  DEF     CHARACTER*1
          Specifies the kind of values used as parameters when
          generating parameter-dependent and scalable examples
          (i.e., examples with NR(1) = 2, 3, or 4):
          DEF = 'D' or 'd': Default values are used.
          DEF = 'N' or 'n': Values set in DPAR and IPAR are used.
          This parameter is not referenced if NR(1) = 1.
          Note that the scaling parameter of examples with
          NR(1) = 3 or 4 is considered as a regular parameter in
          this context.

</PRE>
<B>Input/Output Parameters</B>
<PRE>
  NR      (input) INTEGER array, dimension 2
          Specifies the index of the desired example according
          to [1].
          NR(1) defines the group:
                1 : parameter-free problems of fixed size
                2 : parameter-dependent problems of fixed size
                3 : parameter-free problems of scalable size
                4 : parameter-dependent problems of scalable size
          NR(2) defines the number of the benchmark example
          within a certain group according to [1].

  DPAR    (input/output) DOUBLE PRECISION array, dimension 2
          On entry, if DEF = 'N' or 'n' and the desired example
          depends on real parameters, then the array DPAR must
          contain the values for these parameters.
          For an explanation of the parameters see [1].
          For Example 4.1, DPAR(1) and DPAR(2) define 'r' and 's',
          respectively.
          For Example 4.2, DPAR(1) and DPAR(2) define 'lambda' and
          's', respectively.
          For Examples 4.3 and 4.4, DPAR(1) defines the parameter
          't'.
          On exit, if DEF = 'D' or 'd' and the desired example
          depends on real parameters, then the array DPAR is
          overwritten by the default values given in [1].

  IPAR    (input/output) INTEGER array of DIMENSION at least 1
          On entry, if DEF = 'N' or 'n' and the desired example
          depends on integer parameters, then the array IPAR must
          contain the values for these parameters.
          For an explanation of the parameters see [1].
          For Examples 4.1, 4.2, and 4.3, IPAR(1) defines 'n'.
          For Example 4.4, IPAR(1) defines 'q'.
          On exit, if DEF = 'D' or 'd' and the desired example
          depends on integer parameters, then the array IPAR is
          overwritten by the default values given in [1].

  VEC     (output) LOGICAL array, dimension 8
          Flag vector which displays the availability of the output
          data:
          VEC(1) and VEC(2) refer to N and M, respectively, and are
          always .TRUE.
          VEC(3) is .TRUE. iff E is NOT the identity matrix.
          VEC(4) and VEC(5) refer to A and Y, respectively, and are
          always .TRUE.
          VEC(6) is .TRUE. iff B is provided.
          VEC(7) is .TRUE. iff the solution matrix X is provided.
          VEC(8) is .TRUE. iff the Cholesky factor U is provided.

  N       (output) INTEGER
          The actual state dimension, i.e., the order of the
          matrices E and A.

  M       (output) INTEGER
          The number of rows in the matrix B. If B is not provided
          for the desired example, M = 0 is returned.

  E       (output) DOUBLE PRECISION array, dimension (LDE,N)
          The leading N-by-N part of this array contains the
          matrix E.
          NOTE that this array is overwritten (by the identity
          matrix), if VEC(3) = .FALSE.

  LDE     INTEGER
          The leading dimension of array E.  LDE &gt;= N.

  A       (output) DOUBLE PRECISION array, dimension (LDA,N)
          The leading N-by-N part of this array contains the
          matrix A.

  LDA     INTEGER
          The leading dimension of array A.  LDA &gt;= N.

  Y       (output) DOUBLE PRECISION array, dimension (LDY,N)
          The leading N-by-N part of this array contains the
          matrix Y.

  LDY     INTEGER
          The leading dimension of array Y.  LDY &gt;= N.

  B       (output) DOUBLE PRECISION array, dimension (LDB,N)
          The leading M-by-N part of this array contains the
          matrix B.

  LDB     INTEGER
          The leading dimension of array B.  LDB &gt;= M.

  X       (output) DOUBLE PRECISION array, dimension (LDX,N)
          The leading N-by-N part of this array contains the
          matrix X.

  LDX     INTEGER
          The leading dimension of array X.  LDX &gt;= N.

  U       (output) DOUBLE PRECISION array, dimension (LDU,N)
          The leading N-by-N part of this array contains the
          matrix U.

  LDU     INTEGER
          The leading dimension of array U.  LDU &gt;= N.

  NOTE    (output) CHARACTER*70
          String containing short information about the chosen
          example.

</PRE>
<B>Workspace</B>
<PRE>
  DWORK   DOUBLE PRECISION array, dimension (LDWORK)

  LDWORK  INTEGER
          The length of the array DWORK.
          For Examples 4.1 and 4.2., LDWORK &gt;= 2*IPAR(1) is
          required.
          For the other examples, no workspace is needed, i.e.,
          LDWORK &gt;= 1.

</PRE>
<B>Error Indicator</B>
<PRE>
  INFO    INTEGER
          = 0:  successful exit;
          &lt; 0:  if INFO = -i, the i-th argument had an illegal
                value; in particular, INFO = -3 or -4 indicates
                that at least one of the parameters in DPAR or
                IPAR, respectively, has an illegal value.

</PRE>
<A name="References"><B><FONT SIZE="+1">References</FONT></B></A>
<PRE>
  [1]  D. Kressner, V. Mehrmann, and T. Penzl.
       DTLEX - a Collection of Benchmark Examples for Discrete-
       Time Lyapunov Equations.
       SLICOT Working Note 1999-7, 1999.

</PRE>
<A name="Numerical Aspects"><B><FONT SIZE="+1">Numerical Aspects</FONT></B></A>
<PRE>
  None

</PRE>

<A name="Comments"><B><FONT SIZE="+1">Further Comments</FONT></B></A>
<PRE>
  None
</PRE>

<A name="Example"><B><FONT SIZE="+1">Example</FONT></B></A>
<P>
<B>Program Text</B>
<PRE>
C     BB04AD EXAMPLE PROGRAM TEXT
C
C     .. Parameters ..
      INTEGER          NIN, NOUT
      PARAMETER        (NIN = 5, NOUT = 6)
      INTEGER          NMAX, MMAX
      PARAMETER        (NMAX = 100, MMAX = 100)
      INTEGER          LDE, LDA, LDY, LDB, LDX, LDU, LDWORK
      PARAMETER        (LDE = NMAX, LDA = NMAX, LDY = NMAX, LDB = MMAX,
     1                  LDX = NMAX, LDU = NMAX, LDWORK = 2*NMAX)
C     .. Local Scalars ..
      CHARACTER        DEF
      INTEGER          INFO, N, M, I, J, LDPAR, LIPAR
      CHARACTER*70     NOTE
C     .. Local Arrays ..
      DOUBLE PRECISION E(LDE,NMAX), A(LDA, NMAX), Y(LDY, NMAX),
     1                 B(LDB,NMAX), X(LDX, NMAX), U(LDU, NMAX),
     2                 DPAR(2), DWORK(LDWORK)
      INTEGER          NR(2), IPAR(1)
      LOGICAL          VEC(8)
C     .. External Functions ..
      LOGICAL          LSAME
      EXTERNAL         LSAME
C     .. External Subroutines ..
      EXTERNAL         BB04AD
C     .. Executable Statements ..
      WRITE (NOUT, FMT = 99999)
C     Skip the heading in the data file and read the data.
      READ (NIN, FMT = '()')
      READ (NIN, FMT = *) DEF
      READ (NIN, FMT = *) (NR(I), I = 1, 2)
      IF (LSAME(DEF,'N')) THEN
        READ (NIN, FMT = *) LDPAR
        IF (LDPAR .GT. 0)  READ (NIN, FMT = *) (DPAR(I), I = 1, LDPAR)
        READ (NIN, FMT = *) LIPAR
        IF (LIPAR .GT. 0)  READ (NIN, FMT = *) (IPAR(I), I = 1, LIPAR)
      END IF
C     Generate benchmark example
      CALL BB04AD(DEF, NR, DPAR, IPAR, VEC, N, M, E, LDE, A, LDA, Y,
     1            LDY, B, LDB, X, LDX, U, LDU, NOTE, DWORK, LDWORK,
     2            INFO)
C
      IF (INFO .NE. 0) THEN
        WRITE (NOUT, FMT = 99998) INFO
      ELSE
        WRITE (NOUT, FMT = *) NOTE
        WRITE (NOUT, FMT = 99997) N
        WRITE (NOUT, FMT = 99996) M
        IF (VEC(3)) THEN
          WRITE (NOUT, FMT = 99995)
          DO 10  I = 1, N
            WRITE (NOUT, FMT = 99985) (E(I,J), J = 1, N)
10        CONTINUE
        ELSE
          WRITE (NOUT, FMT = 99994)
        END IF
        WRITE (NOUT,FMT = 99993)
        DO 20  I = 1, N
          WRITE (NOUT, FMT = 99985) (A(I,J), J = 1, N)
20      CONTINUE
        IF (VEC(6)) THEN
          WRITE (NOUT,FMT = 99992)
          DO 30  I = 1, M
            WRITE (NOUT, FMT = 99985) (B(I,J), J = 1, N)
30        CONTINUE
        ELSE
          WRITE (NOUT, FMT = 99991)
        END IF
        WRITE (NOUT,FMT = 99990)
        DO 40  I = 1, N
          WRITE (NOUT, FMT = 99985) (Y(I,J), J = 1, N)
40      CONTINUE
        IF (VEC(7)) THEN
          WRITE (NOUT, FMT = 99989)
          DO 50  I = 1, N
            WRITE (NOUT, FMT = 99985) (X(I,J), J = 1, N)
50        CONTINUE
        ELSE
          WRITE (NOUT, FMT = 99988)
        END IF
        IF (VEC(8)) THEN
          WRITE (NOUT, FMT = 99987)
          DO 60  I = 1, N
            WRITE (NOUT, FMT = 99985) (U(I,J), J = 1, N)
60        CONTINUE
        ELSE
          WRITE (NOUT, FMT = 99986)
        END IF
      END IF
C
99999 FORMAT (' BB04AD EXAMPLE PROGRAM RESULTS', /1X)
99998 FORMAT (' INFO on exit from BB04AD = ', I3)
99997 FORMAT (/' Order of matrix A:            N  = ', I3)
99996 FORMAT (' Number of rows in matrix B:   M  = ', I3)
99995 FORMAT (/' E  = ')
99994 FORMAT (/' E is the identity matrix.')
99993 FORMAT (' A  = ')
99992 FORMAT (' B  = ')
99991 FORMAT (' B is not provided.')
99990 FORMAT (' Y  = ')
99989 FORMAT (' X  = ')
99988 FORMAT (' X is not provided.')
99987 FORMAT (' U  = ')
99986 FORMAT (' U is not provided.')
99985 FORMAT (20(1X,F8.4))
C
      END
</PRE>
<B>Program Data</B>
<PRE>
BB04AD EXAMPLE PROGRAM DATA
N
4 1
2
.15D1
.15D1
1
5
</PRE>
<B>Program Results</B>
<PRE>
 BB04AD EXAMPLE PROGRAM RESULTS

 DTLEX: Example 4.1                                                    

 Order of matrix A:            N  =   5
 Number of rows in matrix B:   M  =   1

 E is the identity matrix.
 A  = 
   0.4562   0.0308   0.1990   0.0861   0.0217
   0.0637   0.5142  -0.1828   0.0096  -0.1148
   0.3139   0.1287   0.3484   0.1653  -0.1975
   0.1500   0.0053  -0.1838   0.2501  -0.0687
   0.0568  -0.1006  -0.3735  -0.0202   0.2285
 B  = 
   0.3086   0.0247  -0.4691   0.1728  -0.3704
 Y  = 
  -0.0953  -0.0076   0.1448  -0.0533   0.1143
  -0.0076  -0.0006   0.0116  -0.0043   0.0091
   0.1448   0.0116  -0.2201   0.0811  -0.1738
  -0.0533  -0.0043   0.0811  -0.0299   0.0640
   0.1143   0.0091  -0.1738   0.0640  -0.1372
 X  = 
   0.0953   0.0076  -0.1448   0.0533  -0.1143
   0.0076   0.0006  -0.0116   0.0043  -0.0091
  -0.1448  -0.0116   0.2201  -0.0811   0.1738
   0.0533   0.0043  -0.0811   0.0299  -0.0640
  -0.1143  -0.0091   0.1738  -0.0640   0.1372
 U is not provided.
</PRE>

<HR>
<p>
<A HREF=..\libindex.html><B>Return to index</B></A></BODY>
</HTML>
